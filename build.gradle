buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

def versionProperties = new Properties();
versionProperties.load(new StringReader(project.file('version.properties').getText('UTF-8')))
version = versionProperties.getOrDefault('modVersion', 'unknown')
group = "com.github.sugarmgp.eoc"
archivesBaseName = "EOC"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    mappings = "snapshot_20171003"

    // 替换EOC版本号
    replaceIn 'EOC.java'
    replace '@EOCVERSION@', project.version
}

dependencies {
    compileOnly 'com.github.zi_jing.cuckoolib:CuckooLib:1.0.5'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        name = 'AliyunMaven'
        url = 'http://maven.aliyun.com/nexus/content/groups/public'
    }
}

// 强制UTF-8构建
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

